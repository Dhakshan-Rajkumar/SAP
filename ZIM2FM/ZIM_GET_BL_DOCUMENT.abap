FUNCTION ZIM_GET_BL_DOCUMENT.
*"----------------------------------------------------------------------
*"*"Local interface:
*"  IMPORTING
*"     VALUE(ZFBLNO) LIKE  ZTBL-ZFBLNO
*"  EXPORTING
*"     VALUE(W_ZTBL) LIKE  ZTBL STRUCTURE  ZTBL
*"  TABLES
*"      IT_ZSBLCST STRUCTURE  ZSBLCST
*"      IT_ZSBLCST_ORG STRUCTURE  ZSBLCST
*"      IT_ZSBLCON STRUCTURE  ZSBLCON
*"      IT_ZSBLCON_ORG STRUCTURE  ZSBLCON
*"      IT_ZSBLCST1 STRUCTURE  ZSBLCST
*"      IT_ZSBLCST1_ORG STRUCTURE  ZSBLCST
*"      IT_ZSBLIT STRUCTURE  ZSBLIT
*"      IT_ZSBLIT_ORG STRUCTURE  ZSBLIT
*"  EXCEPTIONS
*"      NOT_FOUND
*"      NOT_INPUT
*"----------------------------------------------------------------------
DATA : WL_VIA(1)   TYPE   C.

REFRESH : IT_ZSBLCST, IT_ZSBLCON, IT_ZSBLCST_ORG, IT_ZSBLCON_ORG,
          IT_ZSBLCST1,  IT_ZSBLCST1_ORG,
          IT_ZSBLIT,  IT_ZSBLIT_ORG.

  CLEAR : W_ZTBL.

  IF ZFBLNO IS INITIAL.   RAISE NOT_INPUT.   ENDIF.

  SELECT SINGLE * INTO W_ZTBL FROM ZTBL
                              WHERE ZFBLNO EQ ZFBLNO.
  IF SY-SUBRC NE 0.    RAISE NOT_FOUND.   ENDIF.

  IF W_ZTBL-ZFVIA EQ 'VSL'.
     WL_VIA = 'O'.
  ELSEIF W_ZTBL-ZFVIA EQ 'AIR'.
     WL_VIA = 'A'.
  ENDIF.
* B/L 비용( 해외운송비 )
  REFRESH : IT_ZSBLCST.
  SELECT * INTO CORRESPONDING FIELDS OF TABLE IT_ZSBLCST
                FROM ZTBLCST
                WHERE ZFBLNO EQ ZFBLNO
                AND   ZFCSQ  >=  '10000'.

  LOOP AT IT_ZSBLCST.
     W_TABIX = SY-TABIX.
     SELECT SINGLE ZFCDNM ZFCD2
            INTO (IT_ZSBLCST-ZFCDNM, IT_ZSBLCST-ZFCD2)
            FROM ZTIMIMG08
            WHERE ZFCDTY EQ '004'
            AND   ZFCD   EQ IT_ZSBLCST-ZFCSCD.

     IF IT_ZSBLCST-ZFCSCD EQ 'ABC' OR IT_ZSBLCST-ZFCSCD EQ 'OBC'.
         SELECT SINGLE ZFTFA
           INTO IT_ZSBLCST-ZFTFA
           FROM ZTIDR
          WHERE ZFBLNO EQ ZFBLNO
            AND   ZFCLSEQ EQ 1.
     ENDIF.

     MODIFY IT_ZSBLCST INDEX W_TABIX.
  ENDLOOP.
  SORT IT_ZSBLCST BY ZFCD2.
  IT_ZSBLCST_ORG[] = IT_ZSBLCST[].

* B/L 비용 ( 보세운송비 )
  REFRESH : IT_ZSBLCST1.
  SELECT * INTO CORRESPONDING FIELDS OF TABLE IT_ZSBLCST1
                FROM ZTBLCST
                WHERE ZFBLNO EQ ZFBLNO
                AND   ZFCSQ  <   '10000'.

  LOOP AT IT_ZSBLCST1.
     W_TABIX = SY-TABIX.
     SELECT SINGLE ZFCDNM ZFCD2
            INTO (IT_ZSBLCST1-ZFCDNM, IT_ZSBLCST1-ZFCD2)
            FROM ZTIMIMG08
            WHERE ZFCDTY EQ '005'
            AND   ZFCD   EQ IT_ZSBLCST1-ZFCSCD.
     MODIFY IT_ZSBLCST1 INDEX W_TABIX.
  ENDLOOP.
  SORT IT_ZSBLCST1 BY ZFCD2.
  IT_ZSBLCST1_ORG[] = IT_ZSBLCST1[].

* 컨테이너.
  REFRESH : IT_ZSBLCON.
  SELECT * INTO CORRESPONDING FIELDS OF TABLE IT_ZSBLCON
                              FROM ZTBLCON
                              WHERE ZFBLNO EQ ZFBLNO.
  IT_ZSBLCON_ORG[] = IT_ZSBLCON[].

* B/L 자재내역.
  SELECT * INTO CORRESPONDING FIELDS OF TABLE IT_ZSBLIT
                              FROM ZTBLIT
                              WHERE ZFBLNO EQ ZFBLNO.
  SORT IT_ZSBLIT BY ZFBLIT.

  LOOP AT IT_ZSBLIT.
     W_TABIX =  SY-TABIX.
*++++> P/O DATA 조회.
     IF NOT IT_ZSBLIT-EBELN IS INITIAL.
        SELECT SINGLE MENGE UEBTO UEBTK WEPOS ELIKZ LOEKZ UNTTO
                      WERKS LGORT MATKL BPUMN BPUMZ
               INTO (IT_ZSBLIT-MENGE_PO, IT_ZSBLIT-UEBTO,
                     IT_ZSBLIT-UEBTK,    IT_ZSBLIT-WEPOS,
                     IT_ZSBLIT-ELIKZ,    IT_ZSBLIT-LOEKZ,
                     IT_ZSBLIT-UNTTO,    IT_ZSBLIT-WERKS,
                     IT_ZSBLIT-LGORT,    IT_ZSBLIT-MATKL,
                     IT_ZSBLIT-BPUMN,    IT_ZSBLIT-BPUMZ)
               FROM   EKPO
               WHERE  EBELN   EQ   IT_ZSBLIT-EBELN
               AND    EBELP   EQ   IT_ZSBLIT-EBELP.
     ENDIF.
*+++++> 수입의뢰 수량.
     IF NOT IT_ZSBLIT-ZFREQNO IS INITIAL.
        SELECT SINGLE MENGE KBETR KWERT KPEIN KMEIN
               INTO (IT_ZSBLIT-MENGE, IT_ZSBLIT-KBETR,
                     IT_ZSBLIT-KWERT, IT_ZSBLIT-KPEIN,
                     IT_ZSBLIT-KMEIN)
               FROM  ZTREQIT
               WHERE ZFREQNO EQ IT_ZSBLIT-ZFREQNO
               AND   ZFITMNO EQ IT_ZSBLIT-ZFITMNO.

*>>>>> B/L TOTAL 수량(수입의뢰별).
        SELECT SUM( BLMENGE ) INTO IT_ZSBLIT-ZFBLTOT
               FROM  ZTBLIT
               WHERE ZFREQNO  EQ IT_ZSBLIT-ZFREQNO
               AND   ZFITMNO  EQ IT_ZSBLIT-ZFITMNO.
     ENDIF.

*----> SALES ORDER REF.
     IF NOT IT_ZSBLIT-VBELN IS INITIAL.
        SELECT SINGLE KWMENG INTO IT_ZSBLIT-MENGE
               FROM  VBAP
               WHERE VBELN EQ IT_ZSBLIT-VBELN
               AND   POSNR EQ IT_ZSBLIT-POSNR.

*>>>>> B/L TOTAL 수량(S/0 별).
        SELECT SUM( BLMENGE ) INTO IT_ZSBLIT-ZFBLTOT
               FROM  ZTBLIT
               WHERE VBELN EQ IT_ZSBLIT-VBELN
               AND   POSNR EQ IT_ZSBLIT-POSNR.
     ENDIF.
*>> 하역수량.
     SELECT SUM( CGMENGE ) INTO IT_ZSBLIT-CGMENGE
            FROM  ZTCGIT
            WHERE ZFBLNO   EQ IT_ZSBLIT-ZFBLNO
            AND   ZFBLIT   EQ IT_ZSBLIT-ZFBLIT
            AND   CGLOEKZ  NE 'X'.

*>> 상업송장 수량.
     SELECT SUM( ZFPRQN ) SUM( CMENGE )
            INTO (IT_ZSBLIT-ZFPRQN, IT_ZSBLIT-CMENGE)
            FROM  ZTCIVIT
            WHERE ZFBLNO   EQ IT_ZSBLIT-ZFBLNO
            AND   ZFBLIT   EQ IT_ZSBLIT-ZFBLIT.

*>> 통관요청 수량.
     SELECT SUM( CCMENGE ) INTO IT_ZSBLIT-CCMENGE
            FROM ZTIVIT
            WHERE ZFBLNO  EQ IT_ZSBLIT-ZFBLNO
            AND   ZFBLIT  EQ IT_ZSBLIT-ZFBLIT.

*>>   수입승인번호.
     SELECT SINGLE ZFOPNNO INTO IT_ZSBLIT-ZFOPNNO
            FROM   ZTREQHD
            WHERE  ZFREQNO EQ IT_ZSBLIT-ZFREQNO.

     MODIFY  IT_ZSBLIT   INDEX  W_TABIX.

  ENDLOOP.

  IT_ZSBLIT_ORG[] = IT_ZSBLIT[].

ENDFUNCTION.
