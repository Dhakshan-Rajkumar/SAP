FUNCTION ZSAPBF_PPC1_COMP_ORD_READ_NEW .
*"----------------------------------------------------------------------
*"*"Local Interface:
*"  IMPORTING
*"     VALUE(IF_ORDERID) LIKE  PPC_HEAD-ORDERID
*"     VALUE(IT_REPPOINTS) TYPE  PPC_T_REPPOINT
*"     VALUE(I_DATE_LOW) TYPE  BUDAT OPTIONAL
*"     VALUE(I_DATE_HIGH) TYPE  BUDAT OPTIONAL
*"  EXPORTING
*"     VALUE(ET_MATERIAL_COMPONENTS) TYPE  PPC_T_MATERIAL_COMPONENTS
*"     VALUE(ET_REV_MAT_COMPONENTS) TYPE  PPC_T_MATERIAL_COMPONENTS
*"     VALUE(ET_MAT_COMP_VAR) TYPE  PPC_T_MATERIAL_COMPONENTS
*"     VALUE(ET_REV_MAT_COMP_VAR) TYPE  PPC_T_MATERIAL_COMPONENTS
*"     VALUE(ET_ACT_COMP) TYPE  PPC_T_ACTIVITY_COMPONENTS
*"     VALUE(ET_REV_ACT_COMP) TYPE  PPC_T_ACTIVITY_COMPONENTS
*"     VALUE(ET_ACT_COMP_VAR) TYPE  PPC_T_ACTIVITY_COMPONENTS
*"     VALUE(ET_REV_ACT_COMP_VAR) TYPE  PPC_T_ACTIVITY_COMPONENTS
*"     VALUE(ET_REPPOINTS) TYPE  PPC_T_REPPOINTS
*"     VALUE(ET_REV_REPPOINTS) TYPE  PPC_T_REPPOINTS
*"  CHANGING
*"     VALUE(CF_ORDERID) LIKE  PPC_HEAD-ORDERID OPTIONAL
*"     VALUE(C_GJPER) TYPE  CO_GJPER OPTIONAL
*"  EXCEPTIONS
*"      LOCK_ERROR
*"----------------------------------------------------------------------
  DATA: LS_REPPOINTS TYPE PPC_REPPOINTS,
        LS_REV_REPPOINTS TYPE PPC_REPPOINTS.

  DATA: BEGIN OF LS_EXT_MAT_COMP,
          MAT_COMP LIKE PPC_MATERIAL_COMPONENTS,
          HEAD_SALES_DOC TYPE KDAUF,
          HEAD_SALES_DOC_ITEM TYPE KDPOS,
          HEAD_WBS_ELEM TYPE PS_PSP_PNR,
          FLG_REVERSAL TYPE XFLAG,
        END OF LS_EXT_MAT_COMP.

  DATA: BEGIN OF LS_ACT_COMP,
          ACT_COMP LIKE PPC_ACTIVITY_COMPONENTS,
          FLG_REVERSAL TYPE XFLAG,
        END OF LS_ACT_COMP.

  DATA: LT_REV_MAT_COMP TYPE PPC_MATERIAL_COMPONENTS OCCURS 0.
  DATA: LS_REV_MAT_COMP TYPE PPC_MATERIAL_COMPONENTS.

  DATA: LS_REPPOINT LIKE PPC_HEAD-REPPOINT.

  DATA: LS_SUBRC LIKE SY-SUBRC.

  RANGES LT_REPPOINT_RANGES FOR PPC_HEAD-REPPOINT.

  RANGES LT_DATE_RANGES FOR PPC_HEAD-POSTDATE.


* ... Fülle die Rangestabelle für die Zählpunkte
  LOOP AT IT_REPPOINTS INTO LS_REPPOINT.
    MOVE 'I' TO LT_REPPOINT_RANGES-SIGN.
    MOVE 'EQ' TO LT_REPPOINT_RANGES-OPTION.
    MOVE LS_REPPOINT TO LT_REPPOINT_RANGES-LOW.
    APPEND LT_REPPOINT_RANGES.
  ENDLOOP.

* ... Fülle die Rangestabelle für das Datum
  MOVE 'I' TO LT_DATE_RANGES-SIGN.
  IF NOT I_DATE_LOW  IS INITIAL AND
     NOT I_DATE_HIGH IS INITIAL.
    MOVE 'BT' TO LT_DATE_RANGES-OPTION.
  ELSEIF
     NOT I_DATE_LOW  IS INITIAL AND
         I_DATE_HIGH IS INITIAL.
    MOVE 'GE' TO LT_DATE_RANGES-OPTION.
  ELSEIF
         I_DATE_LOW  IS INITIAL AND
     NOT I_DATE_HIGH IS INITIAL.
    MOVE 'LE' TO LT_DATE_RANGES-OPTION.
  ENDIF.
  MOVE I_DATE_LOW  TO LT_DATE_RANGES-LOW.
  MOVE I_DATE_HIGH TO LT_DATE_RANGES-HIGH.
  IF NOT I_DATE_LOW IS INITIAL OR
     NOT I_DATE_HIGH IS INITIAL.
    APPEND LT_DATE_RANGES.
  ENDIF.

* ... Initialisiere die Exporting-Parameter
  REFRESH ET_MATERIAL_COMPONENTS.
  REFRESH ET_REV_MAT_COMPONENTS.
  REFRESH ET_REPPOINTS.
  REFRESH ET_REV_REPPOINTS.
  REFRESH ET_MAT_COMP_VAR.
  REFRESH ET_REV_MAT_COMP_VAR.
  REFRESH ET_ACT_COMP.
  REFRESH ET_ACT_COMP_VAR.
  REFRESH ET_REV_ACT_COMP.
  REFRESH ET_REV_ACT_COMP_VAR.

* ... Sperre das Rückmelden zum aktuellen Auftrag
*  DO gc_orderid_enq_attempts TIMES.

*******************************************************
* Disabled for fixing urgent CO issue in 2/2/2012 {
*******************************************************

****************  CALL FUNCTION 'ENQUEUE_E_PPC_ORDERID'
****************    EXPORTING
****************      MODE_PPC_ORD_INF = 'E'
****************      MANDT            = SY-MANDT
****************      ORDERID          = IF_ORDERID
****************      _SCOPE           = '1'
****************      _WAIT            = 'X'
****************    EXCEPTIONS
****************      FOREIGN_LOCK     = 1
****************      SYSTEM_FAILURE   = 2
****************      OTHERS           = 3.
****************
****************  MOVE SY-SUBRC TO LS_SUBRC.
****************  IF LS_SUBRC EQ 1.
*****************      WAIT UP TO gc_orderid_waittime SECONDS.
****************  ELSE.
*****************      EXIT.
****************  ENDIF.

*******************************************************
*******************************************************

*  ENDDO.
****************  IF LS_SUBRC EQ 1.
****************    MESSAGE E550(PPC1DM) RAISING LOCK_ERROR.
****************  ELSEIF LS_SUBRC EQ 2.
****************    MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
****************          WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4
****************          RAISING LOCK_ERROR.
****************  ENDIF.

*******************************************************
* }
*******************************************************

* ... Ermittle die Zählpunktmengen für die Gutmeldungen
  SELECT REPPOINT SUM( CONFQUANT ) CONFUNIT FROM PPC_HEAD
  INTO TABLE ET_REPPOINTS
  WHERE ORDERID EQ IF_ORDERID AND
        FLG_REVERSAL EQ ' ' AND
        REPPOINT IN LT_REPPOINT_RANGES AND
        POSTDATE IN LT_DATE_RANGES
  GROUP BY REPPOINT
           CONFUNIT.


* ... Ermittle die Zählpunktmengen für die Ausschußmeldungen
  SELECT REPPOINT SUM( CONFQUANT ) CONFUNIT FROM PPC_HEAD
  INTO TABLE ET_REV_REPPOINTS
  WHERE ORDERID EQ IF_ORDERID AND
        FLG_REVERSAL EQ 'X' AND
        REPPOINT IN LT_REPPOINT_RANGES AND
        POSTDATE IN LT_DATE_RANGES
  GROUP BY REPPOINT
           CONFUNIT.

* ... -----------
* ... MATERIALIEN
* ... -----------
  SELECT
    T1~REPPOINT
    T2~MATNR
    T2~WERKS
    T2~LGORT
    T2S~CHARG
    T2~PRVBE
    T2~GMOVE_IND
    T2~SOBKZ
    T2~KZBWS
    T2~KZVBR
    T2S~KDAUF
    T2S~KDPOS
    T2S~PSPNR
    T2S~CALCNR
    SUM( T4~CONFQUANT )
    SUM( T5~DELTA_CONFQUANT )
    T4~CONFUNIT
    T1~KDAUF
    T1~KDPOS
    T1~PSPNR
    T1~FLG_REVERSAL
  INTO
    (LS_EXT_MAT_COMP-MAT_COMP-REPPOINT,
     LS_EXT_MAT_COMP-MAT_COMP-MAT_NUMBER,
     LS_EXT_MAT_COMP-MAT_COMP-PLANT,
     LS_EXT_MAT_COMP-MAT_COMP-STORAGE_LOC,
     LS_EXT_MAT_COMP-MAT_COMP-BATCH,
     LS_EXT_MAT_COMP-MAT_COMP-SUPPLY_AREA,
     LS_EXT_MAT_COMP-MAT_COMP-GMOVE_IND,
     LS_EXT_MAT_COMP-MAT_COMP-SPECIAL_STOCK,
     LS_EXT_MAT_COMP-MAT_COMP-SPECIAL_STOCK_VAL,
     LS_EXT_MAT_COMP-MAT_COMP-CONSUMPT_POSTING,
     LS_EXT_MAT_COMP-MAT_COMP-SALES_DOC,
     LS_EXT_MAT_COMP-MAT_COMP-SALES_DOC_ITEM,
     LS_EXT_MAT_COMP-MAT_COMP-WBS_ELEM,
     LS_EXT_MAT_COMP-MAT_COMP-COSTING_NUM,
     LS_EXT_MAT_COMP-MAT_COMP-QUANTITY,
     LS_EXT_MAT_COMP-MAT_COMP-DELTA_QUANTITY,
     LS_EXT_MAT_COMP-MAT_COMP-UNIT_OF_MEASURE,
     LS_EXT_MAT_COMP-HEAD_SALES_DOC,
     LS_EXT_MAT_COMP-HEAD_SALES_DOC_ITEM,
     LS_EXT_MAT_COMP-HEAD_WBS_ELEM,
     LS_EXT_MAT_COMP-FLG_REVERSAL)
FROM
    ( ( ( PPC_HEAD AS T1 INNER JOIN PPC_CONF_MAT AS T4
        ON T1~HEADID = T4~HEADID )
      LEFT OUTER JOIN PPC_CONF_MAT_VAR AS T5
        ON T4~HEADID = T5~HEADID AND
           T4~ACCID = T5~ACCID )
      INNER JOIN PPC_MAT_DET AS T2S
        ON T4~ACCID = T2S~ACCID )
      INNER JOIN PPC_MAT AS T2
        ON T2S~MATID = T2~MATID
WHERE
      T1~ORDERID EQ IF_ORDERID AND
      ( t2~gmove_ind EQ c_gmove_ind_0 OR        "n.708295
        t2~gmove_ind EQ c_gmove_ind_3 ) AND     "n.708295
      T1~REPPOINT IN LT_REPPOINT_RANGES AND
      T1~POSTDATE IN LT_DATE_RANGES             "Leo
GROUP BY
    T1~REPPOINT
    T2~MATNR
    T2~WERKS
    T2~LGORT
    T2S~CHARG
    T2~PRVBE
    T2~GMOVE_IND
    T2~SOBKZ
    T2~KZBWS
    T2~KZVBR
    T2S~KDAUF
    T2S~KDPOS
    T2S~PSPNR
    T2S~CALCNR
    T4~CONFUNIT
    T1~KDAUF
    T1~KDPOS
    T1~PSPNR
    T1~FLG_REVERSAL
    %_HINTS ORACLE '&SUBSTITUTE LITERALS&'
            MSSQLNT '&SUBSTITUTE LITERALS&'
            DB2     '&SUBSTITUTE LITERALS&'.


* ... Bestimme die Kontierung auf Komponentenebene          "AMF_CONVERT
    IF NOT LS_EXT_MAT_COMP-MAT_COMP-SPECIAL_STOCK IS INITIAL AND
       NOT LS_EXT_MAT_COMP-HEAD_SALES_DOC IS INITIAL AND
           LS_EXT_MAT_COMP-MAT_COMP-SALES_DOC IS INITIAL.
      MOVE LS_EXT_MAT_COMP-HEAD_SALES_DOC TO
           LS_EXT_MAT_COMP-MAT_COMP-SALES_DOC.
    ENDIF.
    IF NOT LS_EXT_MAT_COMP-MAT_COMP-SPECIAL_STOCK IS INITIAL AND
       NOT LS_EXT_MAT_COMP-HEAD_SALES_DOC_ITEM IS INITIAL AND
           LS_EXT_MAT_COMP-MAT_COMP-SALES_DOC_ITEM IS INITIAL.
      MOVE LS_EXT_MAT_COMP-HEAD_SALES_DOC_ITEM TO
           LS_EXT_MAT_COMP-MAT_COMP-SALES_DOC_ITEM.
    ENDIF.
    IF NOT LS_EXT_MAT_COMP-MAT_COMP-SPECIAL_STOCK IS INITIAL AND
       NOT LS_EXT_MAT_COMP-HEAD_WBS_ELEM IS INITIAL AND
           LS_EXT_MAT_COMP-MAT_COMP-WBS_ELEM IS INITIAL.
      MOVE LS_EXT_MAT_COMP-HEAD_WBS_ELEM TO
             LS_EXT_MAT_COMP-MAT_COMP-WBS_ELEM.
    ENDIF.

* Keine Stornobuchung
    IF LS_EXT_MAT_COMP-FLG_REVERSAL IS INITIAL.
      APPEND LS_EXT_MAT_COMP-MAT_COMP TO ET_MATERIAL_COMPONENTS.

* Wenn Abweichungen vorhanden, fülle auch die Abweichungstabelle
      IF NOT LS_EXT_MAT_COMP-MAT_COMP-DELTA_QUANTITY IS INITIAL.
        MOVE LS_EXT_MAT_COMP-MAT_COMP-DELTA_QUANTITY TO
             LS_EXT_MAT_COMP-MAT_COMP-QUANTITY.
        CLEAR LS_EXT_MAT_COMP-MAT_COMP-DELTA_QUANTITY.
        APPEND LS_EXT_MAT_COMP-MAT_COMP TO ET_MAT_COMP_VAR.
      ENDIF.

* Stornobuchung
    ELSE.
      APPEND LS_EXT_MAT_COMP-MAT_COMP TO ET_REV_MAT_COMPONENTS.

* Wenn Abweichungen vorhanden, fülle auch die Abweichungstabelle
      IF NOT LS_EXT_MAT_COMP-MAT_COMP-DELTA_QUANTITY IS INITIAL.
        MOVE LS_EXT_MAT_COMP-MAT_COMP-DELTA_QUANTITY TO
             LS_EXT_MAT_COMP-MAT_COMP-QUANTITY.
        CLEAR LS_EXT_MAT_COMP-MAT_COMP-DELTA_QUANTITY.
        APPEND LS_EXT_MAT_COMP-MAT_COMP TO ET_REV_MAT_COMP_VAR.
      ENDIF.

    ENDIF.

    CLEAR LS_EXT_MAT_COMP.

  ENDSELECT.


* ... ----------
* ... LEISTUNGEN
* ... ----------
  SELECT
    T1~REPPOINT
    T2~RESOURCE_GUID
    T2~MODE_GUID
    SUM( T3~DURATION_VAR )
    SUM( T3~DURATION_FIX )
    SUM( T4~DELTA_DUR_VAR )
    SUM( T4~DELTA_DUR_FIX )
    T3~DURUNIT
    T1~FLG_REVERSAL
  INTO
    (LS_ACT_COMP-ACT_COMP-REPPOINT,
     LS_ACT_COMP-ACT_COMP-RESSOURCE_GUID,
     LS_ACT_COMP-ACT_COMP-MODE_GUID,
     LS_ACT_COMP-ACT_COMP-DURATION_VAR,
     LS_ACT_COMP-ACT_COMP-DURATION_FIX,
     LS_ACT_COMP-ACT_COMP-DELTA_DUR_VAR,
     LS_ACT_COMP-ACT_COMP-DELTA_DUR_FIX,
     LS_ACT_COMP-ACT_COMP-DURUNIT,
     LS_ACT_COMP-FLG_REVERSAL)
FROM
    ( ( PPC_HEAD AS T1 INNER JOIN PPC_CONF_ACT AS T3
        ON T1~HEADID = T3~HEADID )
      LEFT OUTER JOIN PPC_CONF_ACT_VAR AS T4
        ON T3~HEADID = T4~HEADID AND
           T3~ACTID = T4~ACTID )
      INNER JOIN PPC_ACT AS T2
        ON T3~ACTID = T2~ACTID
WHERE
      T1~ORDERID EQ IF_ORDERID AND
*      t1~flg_reversal EQ c_flg_no_reversal AND
      T1~REPPOINT IN LT_REPPOINT_RANGES AND
      T1~POSTDATE IN LT_DATE_RANGES             "Leo
GROUP BY
    T1~REPPOINT
    T2~RESOURCE_GUID
    T2~MODE_GUID
    T3~DURUNIT
    T1~FLG_REVERSAL
    %_HINTS ORACLE '&SUBSTITUTE LITERALS&'
            MSSQLNT '&SUBSTITUTE LITERALS&'
            DB2     '&SUBSTITUTE LITERALS&'.

* Keine Stornobuchung
    IF LS_ACT_COMP-FLG_REVERSAL IS INITIAL.
      APPEND LS_ACT_COMP-ACT_COMP TO ET_ACT_COMP.

* Wenn Abweichungen vorhanden, fülle auch die Abweichungstabelle
      IF NOT LS_ACT_COMP-ACT_COMP-DELTA_DUR_VAR IS INITIAL OR
         NOT LS_ACT_COMP-ACT_COMP-DELTA_DUR_FIX IS INITIAL.
        MOVE LS_ACT_COMP-ACT_COMP-DELTA_DUR_VAR TO
             LS_ACT_COMP-ACT_COMP-DURATION_VAR.
        MOVE LS_ACT_COMP-ACT_COMP-DELTA_DUR_FIX TO
             LS_ACT_COMP-ACT_COMP-DURATION_FIX.
        CLEAR LS_ACT_COMP-ACT_COMP-DELTA_DUR_VAR.
        CLEAR LS_ACT_COMP-ACT_COMP-DELTA_DUR_FIX.
        APPEND LS_ACT_COMP-ACT_COMP TO ET_ACT_COMP_VAR.
      ENDIF.

* Stornobuchung
    ELSE.
      APPEND LS_ACT_COMP-ACT_COMP TO ET_REV_ACT_COMP.

* Wenn Abweichungen vorhanden, fülle auch die Abweichungstabelle
      IF NOT LS_ACT_COMP-ACT_COMP-DELTA_DUR_VAR IS INITIAL OR
         NOT LS_ACT_COMP-ACT_COMP-DELTA_DUR_FIX IS INITIAL.
        MOVE LS_ACT_COMP-ACT_COMP-DELTA_DUR_VAR TO
             LS_ACT_COMP-ACT_COMP-DURATION_VAR.
        MOVE LS_ACT_COMP-ACT_COMP-DELTA_DUR_FIX TO
             LS_ACT_COMP-ACT_COMP-DURATION_FIX.
        CLEAR LS_ACT_COMP-ACT_COMP-DELTA_DUR_VAR.
        CLEAR LS_ACT_COMP-ACT_COMP-DELTA_DUR_FIX.
        APPEND LS_ACT_COMP-ACT_COMP TO ET_REV_ACT_COMP_VAR.
      ENDIF.
    ENDIF.

    CLEAR LS_ACT_COMP.
  ENDSELECT.

* ... Gebe die Rückmeldesperre auf die Auftragsnummer wieder frei

* Disabled for fixing urgent CO issue in 2/2/2012 {
****************  CALL FUNCTION 'DEQUEUE_E_PPC_ORDERID'
****************    EXPORTING
****************      MODE_PPC_ORD_INF = 'E'
****************      MANDT            = SY-MANDT
****************      ORDERID          = IF_ORDERID
****************      _SCOPE           = '1'.
* }

**** Start; Added by James Sung-Kon Kim 2011.03.02
  cf_orderid = if_orderid.
  c_gjper    = i_date_high(4) * 1000 + i_date_high+4(2).
****** End; Added by James Sung-Kon Kim 2011.03.02

ENDFUNCTION.
